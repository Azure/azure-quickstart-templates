/*************************************************************/
/*    SetWebURLs.sas                                         */
/*                                                           */
/*    Changes metadata in the Configuration Manager plug-in  */
/*    Fields that can be changed are:                        */
/*       HostName (machine name)                             */
/*       Port                                                */
/*       Communication Protocol (http or https)              */
/*                                                           */
/*   Used for setting up proxy server, or ssl                */
/*   updated for SAS 9.4 by Erwan Granger                    */
/*                                                           */
/*************************************************************/
options metaserver="{{ metadata_server_host }}"
        metaport={{ metadata_server_port }}
        metauser="{{ metadata_username }}"
        metapass="{{ metadata_password }}";
options  symbolgen;

%Macro SetWebURLs(mode=dryrun,ExtHost=,ExtPort=,ExtProtocol=,EnvMgrExtPort=);
%***  dryrun only reports on findings. no changes made **;
%***  alternative is update ***;

data _null_;
   %*Declare text vars:;
   length uri uri2 conuri conuri10 conuri20 conuri30 component HostName port communicationprotocol name service ExtHostName
   Name1 ExtPort EnvMgrExtPort ExtCommunicationprotocol ExtService Extname $256;
   %*declare Numeric variables;
   length  n x NumApps   8.;

   NumApps = 0;
   n = 0;
   x = 0;
   uri = "";
   uri2 = "";
   conuri = "";
   conuri10 = "";
   conuri20 = "";
   conuri30 = "";
   component = "";
   HostName = "";
   port = "";
   communicationprotocol = "";
   name = "";
   extname = "";
   service = "";
   ExtHostName = "";
   ExtPort ="";
   EnvMgrExtPort = "";
   ExtCommunicationprotocol = "";
   ExtService = "";
   Name1 = "";

   do while (NumApps >= 0);
      %* this loop works because NumApps becomes (-4) when I have found all the things I was looking for. (n becomes too high) *;

      n++1;
      NumApps = metadata_getnobj("omsobj:DeployedComponent?@Name='Registered SAS Application' or @Name='Registered SAS Solution' or @Name='SAS Presentation Theme' or @Name='VisualAnalyticsTransportService REST' or @Name='LASRKeyRegistrationService REST' or @Name='LASRAuthorizationService REST'",n,uri);
      if (numapps ne -4  ) then do;
         rc1 = metadata_getnasn(uri, "DescriptiveComponent", 1, component);
         if (rc1 >= 0) then do;
            rc1 = metadata_getattr(component, "Name", Name);
            put "Processing " Name;
         end;

         Name1 = Name;
         Name = "";
         rc1 = metadata_getnasn(uri, "SourceConnections", 1, conuri10);
         if (rc1 >= 0) then do;
            rc11 = metadata_getattr(conuri10, "Name", Name);
            rc12 = metadata_getattr(conuri10, "Hostname", HostName);
            rc13 = metadata_getattr(conuri10, "Service", Service);
            rc14 = metadata_getattr(conuri10, "Port", port);
            rc15 = metadata_getattr(conuri10, "CommunicationProtocol", CommunicationProtocol);

            rc2 = metadata_getnasn(uri, "SourceConnections", 2, conuri20);
            if (rc2>=0) then do;
               %* There is an external connection defined *;
               rc21 = metadata_getattr(conuri20, "Name", ExtName);
               rc22 = metadata_getattr(conuri20, "Hostname", ExtHostName);
               rc23 = metadata_getattr(conuri20, "Service", ExtService);

               rc24 = metadata_getattr(conuri20, "Port", ExtPort);
               rc25 = metadata_getattr(conuri20, "CommunicationProtocol", ExtCommunicationProtocol);

               %if "&mode"="update" %then %do;
                  put @8 'changing it from "' ExtHostname '" to "' "&ExtHost" '"';
                  rc = metadata_setattr(conuri20, "Hostname", compress("&extHost"));
                  if (rc >= 0) then put @6 'change successful';

                  x = find(Name1, "DP-SAS-Environment-Manager");
                  if (x >= 1) then do;
                     put @8 'changing it from "' ExtPort '" to "' "&EnvMgrExtPort" '"';
                     rc = metadata_setattr(conuri20, "Port", compress("&EnvMgrExtPort"));
                  end;
                  else do;
                     put @8 'changing it from "' ExtPort '" to "' "&Extport" '"';
                     rc = metadata_setattr(conuri20, "Port", compress("&extport"));
                  end;
                  if (rc >= 0) then put @6 'change successful';

                  put @8 'changing it from "' ExtCommunicationProtocol '" to "' "&extprotocol" '"';
                  rc = metadata_setattr(conuri20, "CommunicationProtocol", compress("&extprotocol"));
                  if (rc >= 0) then put @6 'change successful';

                  put @8 'changing it from "' ExtService  '" to "' Service '"';
                  rc = metadata_setattr(conuri20, "Service", trim(service));
                  if (rc >= 0) then put @6 'change successful';
               %end;
            end;
            else if rc2=-4 then do;
               %if "&mode"="update" %then %do;
                  %* this line creates it *;
                  rc = metadata_newobj("TCPIPConnection", uri2, "External URI", "Foundation", uri, "SourceConnections");
                  rc = metadata_getnasn(uri, "SourceConnections", 1, conuri);
                  rc = metadata_getattr(conuri, "Service", service);
                  if (service = 'SASThemeDesigner' or service = 'SASThemeContentService') then do;
                     service = cats("/", service);
                     rc = metadata_setattr(conuri, "Service", service);
                  end;
                  rc = metadata_getnasn(uri, "SourceConnections", 2, conuri30);

                  if (rc >= 0) then do;
                     rc = metadata_setattr(conuri30, "HostName", compress("&ExtHost"));
                     if (rc >= 0) then put @8 'Confirmed: New Host:' "&ExtHost";
                     x = find(Name1, "DP-SAS-Environment-Manager");
                     if (x >= 1) then do;
                        rc = metadata_setattr(conuri30, "Port", compress("&EnvMgrExtPort"));
                        if (rc >= 0) then put @8 'Confirmed: New Port:' "&EnvMgrExtPort";
                     end;
                     else do;
                        rc = metadata_setattr(conuri30, "Port", compress("&ExtPort"));
                        if (rc >= 0) then put @8 'Confirmed: New Port:' "&ExtPort";
                     end;
                     rc = metadata_setattr(conuri30, "CommunicationProtocol", compress("&ExtProtocol"));
                     if (rc >= 0) then put @8 'Confirmed: New Communiction Protocol:' "&ExtProtocol";
                     rc=metadata_setattr(conuri30, "Service", trim(service));
                     if (rc >= 0) then put @8 'Confirmed: New Service:' service;
                     put @8 "New External URL:"  "&ExtProtocol"  '://' "&ExtHost"  ':' "&ExtPort"  Service;
                  end;
               %end;
            end;
         end;
      end;
   end;
run;

%mend;

%SetWebURLs(mode=update,ExtHost={{ external_host_name }},ExtPort={{ external_port }},ExtProtocol={{ external_protocol }},EnvMgrExtPort={{ env_mgr_external_port }});
